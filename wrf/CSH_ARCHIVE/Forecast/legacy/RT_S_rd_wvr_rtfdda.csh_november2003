#!/bin/csh -f

##This shell decodes the data from the ARM Water Vapor Radiometer (*.prf files)
###############################################################################
echo
echo  " ----------------------------------------------------------------------"
echo  " ---------------- WVR decoder starts  ------------------------------"
echo "$0 $argv[*]"
echo  " ----------------------------------------------------------------------"
###############################################################################

set debug = 0

if ( ${#argv} != 2 ) then
	echo "usage: $0 start_date obs_date"
	echo "where start_date is CCYYMMDDHH"
	echo  "d obs_date is CCYYMMDDHH"
	exit ( 4 )
endif

#set echo
set timestamp
setenv SUBSYSTEM WVR_FDDA


## ENVIRONMENT

set CFILE="$MM5HOME/cycle_code/CONFIG_FILES/cshrc_"

## Testing only

if ($debug > 0) then
    setenv MM5HOST GRM
endif

$CheckConfigFiles
set cfstat = $status
if ( $cfstat != 0 ) then
 echo "${SUBSYSTEM} -- Missing ConfigFile -> exiting"
 exit (2)
endif
source ${CFILE}user.mm5sys.${MM5HOST};

set start_date = $1
set obs_date   = $2

## Position of the WVR at ARM Central Facility and time subsampling (0=no sampling)

set latACF =   "36.606"
set lonACF =  "-97.485"
set elvACF =   "316."
set sampling_mn_ACF = "0"

##	Does the directory exist

if ($debug == 0) then
if(-d $GEAPSTMP) then
 if (! -d $GEAPSTMP/RD_WVR) then
  $MustHaveDir $GEAPSTMP/RD_WVR
  ln -s $GEAPSTMP/RD_WVR $RUNDIR/${start_date}/RD_WVR
 endif
else
 $MustHaveDir $RUNDIR/${start_date}/RD_WVR
endif
endif

##	Have we already done this

if ( -e $RUNDIR/${start_date}/${obs_date}_WVR_data.${MM5HOST} ) then
	echo "already decoded the WVR data set"
endif

## Testing only
if ($debug > 0) then
setenv  WVR_DATA_DIR /data/input/wvr
setenv  RUNDIR  /data/cycles/GEAPSO/0
setenv  RD_WVR_EXE /data/fddahome/cycle_code/{EXECUTABLE_ARCHIVE}/rd_wvr.exe
if (! -d $RUNDIR/${start_date}) mkdir $RUNDIR/${start_date}
if (! -d $RUNDIR/${start_date}/RD_WVR) mkdir $RUNDIR/${start_date}/RD_WVR
endif

##	Go to the directory


cd $RUNDIR/${start_date}/RD_WVR
echo "Now working in  $cwd"

## Bring the most recent WVR file

foreach s ( arm wsmr)
if($s == "arm") then
 set wvr_dir = $WVR_DATA_DIR
else
 set wvr_dir = $WVR_DATA_DIR/../wvr_wsmr
endif

ls -1 ${wvr_dir}/*.prf >&! list_files_prf

set FILES = `sort -r -n list_files_prf`

if ($#FILES > 0) then
   set file = $FILES[1]
else
    echo  "No *.prf file was found at ${WVR_DATA_DIR}"
    exit (1)
endif

cp $file .

## date and time dateb= t-60mn, datea=t+59mn

echo "${obs_date} , -1" >! input
${EXECUTABLE_ARCHIVE}/advance_cymdh < input >! output
set dateb = `cat output | cut -c 1-8`
set timeb = `cat output | cut -c 9-10`
set timeb = ${timeb}0000

set datea = `echo ${obs_date} | cut -c 1-8`
set timea = `echo ${obs_date} | cut -c 9-10`
set timea = ${timea}5959

set hh = `echo ${obs_date} | cut -c 9-10`

## Set-up the namelist

cat >! namelist.wvr << EOF

&record1
latitude  = ${latACF},
longitude = ${lonACF},
elevation = ${elvACF},
/

&record2
time_window_min = '${dateb}.${timeb}',
time_window_max = '${datea}.${timea}',
sampling_mn     =  ${sampling_mn_ACF},
/

&record3
print_obs_found = .TRUE.,
debug           = .FALSE.
/
EOF

## Run the decoder

$RD_WVR_EXE -i ${file} -n namelist.wvr -o rd_wvr_${hh} >&! rd_wvr_${hh}_print.out

#	Put output where it is visible

if ( -e $RUNDIR/${start_date}/RD_WVR/${obs_date}_WVR_data.${MM5HOST} ) rm $RUNDIR/${start_date}/RD_WVR/${obs_date}_WVR_data.${MM5HOST}
mv rd_wvr_${hh} $RUNDIR/${start_date}/RD_WVR/WVR.${obs_date}_data.${MM5HOST}.$s
cat rd_wvr_${hh}_print.out >>! $RUNDIR/${start_date}/${start_date}_wvr_print.out.all

#	Clean up house
if ($debug > 0) then
exit (0)
endif

rm input output
rm list_file_prf
rm namelist.wvr
rm rd_wvr*print.out
end

exit ( 0 )
